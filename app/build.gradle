apply plugin: 'com.android.model.application'

def ndkBuild   = '/home/spykid/Android/Ndk/ndk-build'
def libpcapDir = '/home/spykid/Android/Ndk/external/libpcap/'

task buildNative(type: Exec, description: 'Compile JNI source via NDK') {
    commandLine "$ndkBuild", '-C', file('src/main').absolutePath
}

task cleanNative(type: Exec, description: 'Clean JNI object files') {
    commandLine "$ndkBuild", '-C', file('src/main').absolutePath, 'clean'
}

tasks.clean.dependsOn cleanNative

tasks.buildNative.doLast {
    copy {
        from('src/main/libs/') {
            exclude '**/*.so'
        }
        into('src/main/libs/')
        rename('(.+)', 'lib$1.so')
    }

    delete fileTree('src/main/libs/').exclude('**/*.so')
}

tasks.all {
    task ->
        if (task.name.startsWith('compile') && task.name.contains('MainC')) {
            task.enabled = false
        } else if (task.name.startsWith('link')) {
            task.enabled = false
        } else if (task.name.endsWith('SharedLibrary')) {
            task.dependsOn buildNative
        }
}

model {
    android {
        compileSdkVersion = 23
        buildToolsVersion = "23.0.2"

        defaultConfig.with {
            applicationId = "edu.umkc.droidfluid"
            minSdkVersion.apiLevel = 9
            targetSdkVersion.apiLevel = 17
        }
    }

    android.ndk {
        // Toggle as need
        // stl = "gnustl_shared"
        moduleName = "ofcontroller"

        cppFlags += "-std=c++11"

        cppFlags += "-I${file("$libpcapDir")}".toString()
        cppFlags += "-I${file("src/main/prebuilt/libfluid_msg/")}".toString()
        cppFlags += "-I${file("src/main/prebuilt/libfluid_base/")}".toString()
        cppFlags += "-I${file("src/main/prebuilt/libevent/include/")}".toString()

        ldLibs += "log"
    }

    android.sources{
        main.jniLibs {
            source {
                srcDir 'src/main/libs'
            }
        }
    }

    android.buildTypes {
        release {
            minifyEnabled = false
            proguardFiles += file("proguard-rules.pro")
        }
    }
}


dependencies {
}
